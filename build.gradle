
buildscript {
    ext.kotlin_version = '1.0.5'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
    flatDir {
        dirs "$rootDir/idea-redist/forms"
        dirs "$rootDir/idea-redist/uiDesigner"
    }

}

sourceSets.main.java        { srcDir 'src' }
sourceSets.test.java        { srcDir 'test' }
sourceSets.test.resources   { srcDir 'test-data' }

mainClassName = 'com.probe.usb.host.pc.ProbeUsbHost'

configurations {
    buildIdeaFormsTask
}


dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.github.nyholku:purejavacomm:v1.0.1'
    compile group: 'com.intellij', name: 'uiDesigner'
    compile group: 'com.intellij', name: 'forms_rt'

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    buildIdeaFormsTask name: 'javac2'
    buildIdeaFormsTask name: 'jdom'
    buildIdeaFormsTask name: 'asm-all'
    buildIdeaFormsTask name: 'jgoodies-forms'

}


task compileIdeaForms(dependsOn: configurations.compile.getTaskDependencyFromProjectDependency(true, 'jar')) {
    doLast {
        project.sourceSets.main.output.classesDir.mkdirs()
        ant.taskdef name: 'javac2', classname: 'com.intellij.ant.Javac2', classpath: configurations.buildIdeaFormsTask.asPath
        ant.javac2 srcdir: project.sourceSets.main.java.srcDirs.join(':'),
                classpath: project.sourceSets.main.compileClasspath.asPath,
                destdir: project.sourceSets.main.output.classesDir,
                source: sourceCompatibility,
                target: targetCompatibility,
                includeAntRuntime: false
    }
}

jar.dependsOn compileIdeaForms


